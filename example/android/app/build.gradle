plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def dexprotectorDir = file("${rootProject.projectDir}/dexprotector")
def hasDexProtector = dexprotectorDir.exists() && dexprotectorDir.listFiles()?.any {
    it.name.contains("dexprotector") && it.name.endsWith(".jar")
}

if (hasDexProtector) {
    apply plugin: 'dexprotector'
}

android {
    namespace = "com.example.test_library"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "com.example.test_library"
        minSdk = 23
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.debug

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            debuggable false
            renderscriptDebuggable false

            def dexprotectorConfig = file("${projectDir}/dexprotector-config.xml")
            if (dexprotectorConfig.exists()) {
                ext.set("dexprotector.configFile", dexprotectorConfig.absolutePath)
            }
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    testRuntimeOnly 'org.jetbrains.kotlin:kotlin-test-junit:1.8.22'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'org.mockito:mockito-inline:4.11.0'
    implementation 'org.slf4j:slf4j-nop:1.7.36'
}

tasks.withType(Test).configureEach {
    ignoreFailures = false
    testLogging {
        events "passed", "skipped", "failed"
    }
    outputs.upToDateWhen { false }
}
tasks.named("check") {
    dependsOn("testDebugUnitTest")
}
